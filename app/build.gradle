/*
 * Copyright (c) 2014, Thoughtworks Inc
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * 1. Redistributions of source code must retain the above copyright notice, this
 *    list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright notice,
 *    this list of conditions and the following disclaimer in the documentation
 *    and/or other materials provided with the distribution.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
 * ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
 * WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 * DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR
 * ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
 * (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
 * LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
 * ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
 * (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *
 * The views and conclusions contained in the software and documentation are those
 * of the authors and should not be interpreted as representing official policies,
 * either expressed or implied, of the FreeBSD Project.
 */

def buildNumber = System.getenv('BUILD_NUMBER') as Integer ?: 0

buildscript {
    repositories {
        maven { url 'http://download.crashlytics.com/maven' }
    }

    dependencies {
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'crashlytics'

repositories {
    maven { url 'http://download.crashlytics.com/maven' }
    flatDir {
        dirs 'libs'
    }
}


android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19

        versionCode buildNumber
        versionName "1.8.${buildNumber}"
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE'
    }

    productFlavors {
        dev {
            applicationId 'org.clintonhealthaccess.lmis.app.dev'
        }
        qa {
            applicationId 'org.clintonhealthaccess.lmis.app'
        }
        production {
            applicationId 'org.clintonhealthaccess.lmis.app'
        }
    }

    lintOptions {
        htmlReport true
        htmlOutput file("build/reports/lint/lint-report.html")
        xmlOutput file("build/reports/lint/lint-report.xml")
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    sourceSets {
        androidTest {
            setRoot('src/test')
        }
    }
}
apply plugin: 'android-unit-test'
apply plugin: "jacoco"

jacoco {
    toolVersion = "0.7.1.201405082137"
}

def coverageSourceDirs = ['src/main/java']

task jacocoTestReport(type: JacocoReport, dependsOn: "testQaDebug") {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    classDirectories = fileTree(dir: './build/intermediates/classes/qa/debug/org')
    sourceDirectories = files(coverageSourceDirs)
    executionData = files('build/jacoco/testQaDebug.exec')
}

apply plugin: 'pmd'

check.dependsOn 'pmd'

task pmd(type: Pmd) {
    targetJdk = TargetJdk.VERSION_1_7
    ignoreFailures = true

    ruleSetFiles = files('config/pmd/chai-rulesets.xml')

    source 'src/main'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'org.hamcrest:hamcrest-all:1.3',
            'junit:junit:4.11',
            'org.mockito:mockito-all:1.8.4',
            'com.github.dreamhead:moco-core:0.9.2',
            'org.robolectric:robolectric:2.3', 'com.squareup:fest-android:1.0.8'
    androidTestCompile 'org.hamcrest:hamcrest-all:1.3',
            'junit:junit:4.11',
            'org.mockito:mockito-all:1.8.4',
            'com.github.dreamhead:moco-core:0.9.2',
            'org.robolectric:robolectric:2.3', 'com.squareup:fest-android:1.0.8'
    compile('org.roboguice:roboguice:3.0-alpha-2') {
        exclude module: 'asm'
    }
    compile 'com.google.guava:guava:14.0.1',
            'org.apache.commons:commons-lang3:3.3.2',
            'com.google.code.gson:gson:2.2.4',
            'com.j256.ormlite:ormlite-android:4.48',
            'com.android.support:appcompat-v7:19.1.+',
            'de.greenrobot:eventbus:2.2.1',
            'com.squareup.retrofit:retrofit:1.6.1'
    compile('com.squareup.retrofit:converter-simplexml:1.6.1') {
        exclude group:'stax', module:'stax-api';
        exclude group:'stax', module:'stax';
        exclude group:'xpp3', module:'xpp3'
    }
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.crashlytics.android:crashlytics:1.+'
    compile 'net.danlew:android.joda:2.4.1'
    provided "org.projectlombok:lombok:1.12.6"
    compile project(":dhis2-models")
}
